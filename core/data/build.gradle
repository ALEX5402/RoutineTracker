plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.app.cash.sqldelight)
}

sqldelight {
    databases {
        Database { // This will be the name of the generated database class.
            packageName = "com.rendox.performancetracker"
        }
    }
}

android {
    namespace 'com.rendox.routinetracker'
    compileSdk 33

    defaultConfig {
        testInstrumentationRunner "com.rendox.routinetracker.InstrumentationTestRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    flavorDimensions = ["minSdk"]
    productFlavors {
        minSdk26 {
            dimension "minSdk"
            minSdk 26
        }
        minSdk21 {
            dimension "minSdk"
            minSdk 21
            compileOptions {
                coreLibraryDesugaringEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    coreLibraryDesugaring libs.android.tools.desugar

    implementation project(":core:commonlogic")

    // SQLDelight
    implementation libs.app.cash.sqldelight.android.driver
    implementation libs.app.cash.sqldelight.coroutines.extensions.jvm
    implementation libs.app.cash.sqldelight.primitive.adapters

    // Koin
    implementation platform(libs.insert.koin.bom)
    implementation "io.insert-koin:koin-android"
    implementation "io.insert-koin:koin-test"
    implementation "io.insert-koin:koin-android-test"

    implementation libs.jetbrains.kotlinx.datetime

    implementation libs.androidx.core.ktx
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit

    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation "androidx.compose.ui:ui-test"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    androidTestImplementation libs.google.truth
}